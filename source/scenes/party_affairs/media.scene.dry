title: Media
new-page: true
is-card: true
tags: party_affairs
view-if: media_timer <= 0 and newspaper_seized != 1
on-arrival: media_timer = 1;
card-image: img/Media.jpg
#subtitle: Setting the direction for our newspapers and radio.

= Media

The [+ psi_name +] runs a veritable media empire, with many newspapers throughout every Italian city and region. We could use the media for various purposes...

- @commercial
- @campaign
- @ideological
- @radio
- @radio_2
- @radio_3
- @return: We should not do anything with media at the moment.
- @easy_discard
- @cancel_advisor_action

@commercial
title: Make our newspapers more commercialized by broadening their appeal beyond party supporters.
subtitle: This might upset ideological purists, but it may bring in more funds and perhaps expose the middle class to socialist views.
on-arrival: resources += 10; middle_class_psi += 20; socialist_sentiment += 5; pcdi_gramsci += 1;

Our commercialized newspapers are able to bring in more funds. However, the left and labour factions express their displeasure.

@campaign
title: The purpose of our media is to elect more socialists.
subtitle: Our newspapers will focus on the electoral campaign.
on-arrival: campaign_media += 1; workers_psi += 20; socialist_sentiment += 5;

Our campaign media is mostly reaching the workers who are already saturated with Socialist propaganda, but it must be doing *something*, or at least that is what we hope.

@ideological
title: We will have vibrant ideological debate within our newspapers.
subtitle: There will be space for all factions to air their views.
on-arrival: communist_dissent -= 20; labour_dissent -= 20; maximalist_dissent -= 20; reformist_dissent -= 20; syndicalist_dissent -= 20 if syndicalist_active

This has been our media course historically, and it has successfully paved over the many tensions in our party.

@radio
title: Why not fund a radio station?
subtitle: -3 resources
choose-if: resources >= 3
unavailable-subtitle: Not enough resources. (Need 3)
view-if: radio = 0
on-arrival: radio += 2; resources -= 3; middle_class_psi += 20; industrialists_psi += 20; workers_psi += 20; socialist_sentiment += 5; nationalist_sentiment -= 5; republican_sentiment += 5; pcdi_gramsci += 1;

A radio station would be one of the first of its kind in Italy, and the first socialist station in the nation. Funding is expensive, and radio's in it of itself seem only affordable to the richest of the middle class, alongside industrialists and landowners, who will be the main consumers of our content, it seems.

@radio_2
title: We must keep on building up our radio network.
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources. (Need 1)
view-if: radio > 0 and radio <= 3
on-arrival: radio += 2; resources -= 1; industrialists_psi += 20; middle_class_psi += 20; workers_psi += 20; socialist_sentiment += 5; nationalist_sentiment -= 5; republican_sentiment += 5; pcdi_gramsci += 1;

Our new radio network has a mix of popular and political programming, and is proving a success.

@radio_3
title: We must keep on building up our radio network.
subtitle: It is now financially self-sufficient, and does not require any more resources.
view-if: radio > 3
on-arrival: industrialists_psi += 20; middle_class_psi += 20; workers_psi += 20; rural_psi += 20; veterans_psi += 20; socialist_sentiment += 5; nationalist_sentiment -= 5; republican_sentiment += 5; pcdi_gramsci += 1;

Our radio network has become a successful tool for spreading our message.
